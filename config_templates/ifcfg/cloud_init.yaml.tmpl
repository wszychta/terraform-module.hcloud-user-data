#cloud-config
%{ if length(additional_users) > 0 ~}
users:
%{ for user in additional_users ~}
%{ if user.username != "" && user.username != null ~}
- name: ${ user.username }
%{ endif ~}
%{ if user.username != "" && user.username != null && user.sudo_options != "" && user.sudo_options != null ~}
  sudo: ${ user.sudo_options }
%{ endif ~}
%{ if user.username != "" && user.username != null && length(user.ssh_public_keys) > 0 ~}
  ssh_authorized_keys:
%{ for ssh_key in user.ssh_public_keys ~}
    - ${ ssh_key }
%{ endfor ~}
%{ endif ~}
%{ endfor ~}
%{ endif ~}

# Setup VM timezone
timezone: ${ timezone }

%{ if additional_hosts_entries_file_base64 != "" || length(additional_write_files) > 0 || length(ifcfg_network_config_files) > 0 ~}
write_files:
%{ for network_config in ifcfg_network_config_files ~}
- encoding: b64
  content: ${ network_config.ifcfg }
  owner: root:root
  path: ${ ifcfg_network_root_directory }/ifcfg-${ network_config.device_id }
  permissions: '0644'
%{ if network_config.routes != "" ~}
- encoding: b64
  content: ${ network_config.routes }
  owner: root:root
  path: ${ ifcfg_network_root_directory }/route-${ network_config.device_id }
  permissions: '0644'
%{ endif ~}
%{ endfor ~}
%{ if additional_hosts_entries_file_base64 != "" ~}
# additional hosts file
- encoding: b64
  content: ${ additional_hosts_entries_file_base64 }
  owner: root:root
  path: ${ additional_hosts_entries_file_path }
  permissions: '0644'
%{ endif ~}
%{ if length(additional_write_files) > 0 ~}
# Create additional files defined in terraform
%{ for file in additional_write_files ~}
- encoding: b64
  content: ${ base64encode(file.content) }
  owner: ${file.owner_user}:${file.owner_group}
  path: ${ file.destination }
  permissions: ${ file.permissions }
%{ endfor ~}
%{ endif ~}
%{ endif ~}
%{ if additional_hosts_entries_file_base64 != "" || length(additional_run_commands) > 0 ~}

runcmd:
%{ if additional_hosts_entries_file_base64 != "" ~}
  # Inject hosts variables on first boot
  - cat ${ additional_hosts_entries_file_path } >> /etc/hosts
  - cat ${ additional_hosts_entries_file_path } >> /etc/cloud/templates/hosts.rhel.tmpl
%{ endif ~}
%{ if length(additional_run_commands) > 0 ~}
  # User defined additional commands
%{ for command in additional_run_commands ~}
  - ${ command }
%{ endfor ~}
%{ endif ~}
%{ endif ~}
%{ if upgrade_all_packages == true ~}
# Upgrade system
package_upgrade: true
%{ endif ~}
%{ if reboot_instance == true || upgrade_all_packages == true || length(ifcfg_network_config_files) > 0 ~}
power_state:
  mode: reboot
  delay: "now"
  message: Reboot the machine after successfull cloud-init run with custom cloud-config file
%{ endif ~}